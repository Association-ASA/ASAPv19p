//%attributes = {"invisible":true,"published4DMobile":{"scope":"none"},"publishedWeb":false,"publishedSoap":false,"publishedWsdl":false,"shared":false,"publishedSql":false,"executedOnServer":false,"preemptive":"indifferent"}
$indexTypeMap:=New collection:C1472
$indexTypeMap[1]:="b-tree"
$indexTypeMap[3]:="cluster"
$indexTypeMap[7]:="auto"
$indexTypeMap[8]:="object"

ARRAY LONGINT:C221($types;0)

  //1-field
Begin SQL
	SELECT DISTINCT(_USER_INDEXES.INDEX_TYPE)
	FROM _USER_CONS_COLUMNS,_USER_COLUMNS,_USER_CONSTRAINTS,_USER_INDEXES,_USER_IND_COLUMNS
	WHERE _USER_CONSTRAINTS.CONSTRAINT_TYPE= '4DR'
	AND _USER_CONSTRAINTS.CONSTRAINT_ID = _USER_CONS_COLUMNS.CONSTRAINT_ID
	AND _USER_COLUMNS.TABLE_ID = _USER_CONSTRAINTS.RELATED_TABLE_ID
	AND _USER_COLUMNS.COLUMN_ID = _USER_CONS_COLUMNS.RELATED_COLUMN_ID
	AND _USER_COLUMNS.TABLE_ID = _USER_IND_COLUMNS.TABLE_ID
	AND _USER_COLUMNS.COLUMN_ID = _USER_IND_COLUMNS.COLUMN_ID
	AND _USER_INDEXES.INDEX_ID = _USER_IND_COLUMNS.INDEX_ID
	INTO :$types;
End SQL

$types1:=New collection:C1472
ARRAY TO COLLECTION:C1563($types1;$types)

C_LONGINT:C283($type)

ARRAY TEXT:C222($fields;0)

$results:=New collection:C1472

For each ($type;$types1)
	
	Begin SQL
		SELECT DISTINCT(CONCAT(CONCAT(CONCAT('[:',_USER_COLUMNS.TABLE_ID),']:'),_USER_COLUMNS.COLUMN_ID))
		FROM _USER_CONS_COLUMNS,_USER_COLUMNS,_USER_CONSTRAINTS,_USER_INDEXES,_USER_IND_COLUMNS
		WHERE _USER_CONSTRAINTS.CONSTRAINT_TYPE= '4DR'
		AND _USER_CONSTRAINTS.CONSTRAINT_ID = _USER_CONS_COLUMNS.CONSTRAINT_ID
		AND _USER_COLUMNS.TABLE_ID = _USER_CONSTRAINTS.RELATED_TABLE_ID
		AND _USER_COLUMNS.COLUMN_ID = _USER_CONS_COLUMNS.RELATED_COLUMN_ID
		AND _USER_COLUMNS.TABLE_ID = _USER_IND_COLUMNS.TABLE_ID
		AND _USER_COLUMNS.COLUMN_ID = _USER_IND_COLUMNS.COLUMN_ID
		AND _USER_INDEXES.INDEX_ID = _USER_IND_COLUMNS.INDEX_ID
		AND _USER_INDEXES.KEYWORD = FALSE
		AND _USER_INDEXES.INDEX_TYPE = :$type
		INTO :$fields;
	End SQL
	
	$results.push(New collection:C1472(\
		$indexTypeMap[$type];String:C10(Size of array:C274($fields))).join("\t"))
	
End for each 

SET TEXT TO PASTEBOARD:C523($results.join("\r"))
